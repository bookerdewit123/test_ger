# Struct encapsulating a WsfMessage and suporting metadata
script_struct CommMessage
   script_variables
      # The ID for the message
      int mId = 0;
      
      # The platform that is the source of the mMessage
      WsfPlatform mSourcePlatform = null;
      
      # The platform that is the target of the mMessage
      WsfPlatform mTargetPlatform = null;
      
      # A key/value containing the mMessage
      WsfMessage mMessage = null;
      
      # The simulation time when the commMessage will be handled
      double mHandleTime = -1.0;  
      
      # Whether this CommMessage instance is valid 
      bool mIsValid = false;
      
      # Whether a platform that receives this message should resend it on all of its links
      bool mBroadcast = false;
      
      # The names of the platforms in this messaegs route
      Array<string> mRoute = Array<string>(); 
      
      # The simulation time when the message was created
      double mCreationTime = -1.0;
      
   end_script_variables
   
   script int GetId()
      return mId;
   end_script
   
   script double GetMessageHandlingTime()
      return mHandleTime;
   end_script
   
   script WsfPlatform GetSourcePlatform()
      return mSourcePlatform;
   end_script
   
   script WsfPlatform GetTargetPlatform()
      return mTargetPlatform;
   end_script
   
   script WsfMessage GetMessage()
      return mMessage;
   end_script
   
   script bool IsValid()
      return mIsValid;
   end_script
   
   script bool IsBroadcast()
      return mBroadcast;
   end_script
   
   script int GetHopCount()
      return mRoute.Size() -1;
   end_script
   
   script Array<string> GetRoute()
      return Array<string>(mRoute);
   end_script
   
   script double GetMessageCreationTime()
      return mCreationTime;
   end_script
end_script_struct

# Factory script for creating and initializing a CommMessage struct
#
# param aTimestamp      The simulation time at which the message was generated
# param aSourcePlatform The platform that is sourcing the message
# param aTargetPlatform The platform that is the target of the message
# param aMessage        The message being sent as a Map
script CommMessage ConstructCommMessage( double aTimestamp,
                                         WsfPlatform aSourcePlatform,
                                         WsfMessage aMessage,
                                         bool aBroadcast )
   # Initialize the message counter
   static int nextId = 1;
   
   # Create Invalid CommMessage struct
   CommMessage instance = CommMessage();

   # Validate arguments
   if(aSourcePlatform.IsNull()){
      # TODO log failure
      return instance;
   }

   if(aMessage.IsNull()){
      # TODO log failure
      return instance;
   }
   
   # Create and Initialize CommMessage struct
   instance.mHandleTime      = aTimestamp;
   instance.mCreationTime    = aTimestamp;
   instance.mSourcePlatform  = aSourcePlatform;
   instance.mTargetPlatform  = null;
   instance.mMessage         = aMessage;
   instance.mIsValid         = true;
   instance.mId              = nextId;
   instance.mBroadcast       = aBroadcast;
   instance.mRoute           = { aSourcePlatform.Name() };
   
   nextId += 1;
   
   return instance;
end_script

# Factory script for creating a repeated copy of a CommMessage struct
#
# param aCommMessage    The message to be repeated
# param aTimestamp      The simulation time at which the repeated message was generated
# param aSourcePlatform The platform that is sourcing the message
# param aTargetPlatform The platform that is the target of the message
script CommMessage ConstructCommMessageCopy( CommMessage aCommMessage,
                                             double aTimestamp,
                                             WsfPlatform aSourcePlatform,
                                             WsfPlatform aTargetPlatform ) {
   # Create Invalid CommMessage struct
   CommMessage instance = CommMessage();

   # Validate arguments
   if(aSourcePlatform.IsNull()){
      # TODO log failure
      return instance;
   }
   if(aTargetPlatform.IsNull()){
      # TODO log failure
      return instance;
   }
   
   # Create and Initialize CommMessage struct
   instance.mHandleTime      = aTimestamp;
   instance.mCreationTime    = aCommMessage.GetMessageCreationTime();
   instance.mSourcePlatform  = aSourcePlatform;
   instance.mTargetPlatform  = aTargetPlatform;
   instance.mMessage         = WsfMessage(aCommMessage.GetMessage());
   instance.mIsValid         = true;
   instance.mId              = aCommMessage.GetId();
   instance.mBroadcast       = aCommMessage.IsBroadcast();
   
   # extend the route to include the new target
   Array<string> route = aCommMessage.GetRoute();
   route.PushBack(aTargetPlatform.Name());
   instance.mRoute = route;
   
   return instance;
}
end_script