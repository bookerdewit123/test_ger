# File generated by Wizard 2.2205.6 on Dec 4, 2025.

# Calculate the Pk for a weapon that utilizes No Escape Zones
#
# param rangeNm        The range in nautical miles between the shooter and the target
# param shortRangeNm   The range in nautical miles to the start of the No Escape Zone for the weapon
# param mediumRangeNm  The range in nautical miles to the end of the No Escape Zone for the weapon
# param longRangeNm    The maximum range of the weapon
# param shortRangePk   The Pk for the weapon at short range
# param mediumRangePk  The Pk for the weapon at medium range
# param longRangePk    The Pk for the weapon at long range
script double calculatePk( double rangeNm,
                           double shortRangeNm, double mediumRangeNm, double longRangeNm, 
                           double shortRangePk, double mediumRangePk, double longRangePk ) {
   double shortRangePkSlope  = (1.0 - shortRangePk) / (shortRangeNm - 0.0);
   double mediumRangePkSlope = (shortRangePk - mediumRangePk) / (mediumRangeNm - shortRangeNm);
   double longRangePkSlope   = (mediumRangePk - longRangePk) / (longRangeNm - mediumRangeNm);
   
   double pk = 0.0;
   if( rangeNm < shortRangeNm ) {
      pk = shortRangePk + (shortRangeNm -  rangeNm) * shortRangePkSlope;
   } else if( rangeNm < mediumRangeNm ) {
      pk = mediumRangePk + (mediumRangeNm -  rangeNm) * mediumRangePkSlope;
   } else if( rangeNm < longRangeNm ) {
      pk = longRangePk + (longRangeNm -  rangeNm) * longRangePkSlope;
   }
   
   return pk;
}
end_script                    

# Calculate the Pk for the AIM-120
#
# param rangeNm        The range in nautical miles between the shooter and the target
script double CalculateAim120Pk(double rangeNm) {
   static double shortRangeNm  = 15.0;
   static double mediumRangeNm = 40.0;
   static double longRangeNm   = 86.0;
   
   static double shortRangePk  = 0.80;
   static double mediumRangePk = 0.59;
   static double longRangePk   = 0.00;
   
   return calculatePk( rangeNm, 
                       shortRangeNm, mediumRangeNm, longRangeNm, 
                       shortRangePk, mediumRangePk, longRangePk );
}
end_script

# Calculate the Pk for the AA-13
#
# param rangeNm        The range in nautical miles between the shooter and the target
script double CalculateAA13Pk(double rangeNm) {
   static double shortRangeNm  = 21.5;
   static double mediumRangeNm = 27.0;
   static double longRangeNm   = 124.0;
   
   static double shortRangePk  = 0.50;
   static double mediumRangePk = 0.60;
   static double longRangePk   = 0.00;
   
   return calculatePk( rangeNm, 
                       shortRangeNm, mediumRangeNm, longRangeNm, 
                       shortRangePk, mediumRangePk, longRangePk );
}
end_script