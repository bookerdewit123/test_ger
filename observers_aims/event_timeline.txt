include_once observers_aims\observer_functions.txt

script_variables
   int globalTrackNumber = 0;
   Array<string> eventData = Array<string>();               # The information that is output to the csvgit
   string outputPath = "output#event__timeline_output_" + (string)OVERALL_RUN_NUMBER;    # The file location the csv should be output to
   Array<string> eventDataHeaders = {                       # The list of columns, in the order you want them to appear. You can restructure the output file here.
         "TIME", "PLATFORM", "PLATFORM LATITUDE", "PLATFORM LONGITUDE", "TRACK ID", "TARGET", "TARGET LATITUDE", "TARGET LONGITUDE", "EVENT"};
end_script_variables
# Other ideas from Scott for future implementation
# PLATFORM_TYPE | TARGET_TYPE | aTrack.TrackQuality() | aTrack.Latitude() | aTrack.Longitude()

# Function used to add an event to the csv without the use of observers
script void manuallyRecordEvent(string anEvent)
# @param string anEvent: The event to record, as a string.
   eventData.PushBack(anEvent);
end_script

# Construct anEventData map that can be added to the timeline via storeEventToEventTimeline later.
# This map will contain N/A for most fields.
# It will auto-populate the TIME_NOW.
script Map<string, Object> constructBlankEventData()
# @return: toReturn: anEventData map will default values.

   Map<string, Object> toReturn = Map<string, Object>();
   for (int i = 0; i < eventDataHeaders.Size(); i += 1)
   {
      toReturn.Set(eventDataHeaders[i], "N/A");
   }
   toReturn.Set("TIME", (string)TIME_NOW);
   return toReturn;   
end_script

# Store the anEventData file to the eventData timeline.
# The full dataset will be output to a csv file when the simulation completes.
script void storeEventToEventTimeline(Map<string, Object> anEventData)
# @param Map<string, Object> anEventData: The data for a single event. This will be added to the timeline.
   
   Array<string> dataArray = Array<string>();
   for (int i = 0; i < eventDataHeaders.Size(); i += 1)
   {
      dataArray.PushBack((string)anEventData.Get(eventDataHeaders[i]));
   }
   eventData.PushBack(",".Join(dataArray)); 
end_script

# Write an event to the eventTimeline.
script void recordToEventTimeline(WsfPlatform aPlatform, WsfPlatform aTarget, string aTrackId_str, WsfGeoPoint aTargetLocation, string aStatus)
# @param WsfPlatform aPlatform: The main platform for this event. If none, then enter null.
# @param WsfPlatform aTarget: The other platform for this event. If none, enter null.
# @param WsfTrackId aTrackId: The relevant track. If none, then enter null
# @param string aTargetLocation: The location to use for the target. (Could be either track location, Target.Location(), blank, or something else)
# @param string aStatus: The 'type' of event to record.
   Map<string, Object> anEventData = constructBlankEventData();
   
   if (aPlatform.IsValid())
   {
      anEventData.Set("PLATFORM",            aPlatform.Name());
#      anEventData.Set("SIDE",                aPlatform.Side());
      anEventData.Set("PLATFORM LATITUDE",   aPlatform.Latitude());
      anEventData.Set("PLATFORM LONGITUDE",  aPlatform.Longitude());
   }
   
   if (aTrackId_str != "")
   {
      anEventData.Set("TRACK ID",            aTrackId_str);
   }
   
   if (aTarget.IsValid())
   {
      anEventData.Set("TARGET",              aTarget.Name());
   }
   
   if (aTargetLocation.IsValid())
   {
      anEventData.Set("TARGET LATITUDE",     aTargetLocation.Latitude());
      anEventData.Set("TARGET LONGITUDE",    aTargetLocation.Longitude());
   }
   
   anEventData.Set("EVENT",               aStatus);
   
   storeEventToEventTimeline(anEventData);
end_script

script void eventTimeline_WeaponFired(WsfWeaponEngagement aWeaponEngagement, WsfTrack aTargetTrack)
   recordToEventTimeline(aWeaponEngagement.FiringPlatform(), 
                         aTargetTrack.Target(), 
                          (string) aTargetTrack.GlobalTrackNumber(), 
                         aTargetTrack.LocationAtTime(TIME_NOW), 
                         "WEAPON FIRED");
                         aWeaponEngagement.WeaponPlatform().SetAuxData("GlobalTrackNumber", aTargetTrack.GlobalTrackNumber());
end_script

script void eventTimeline_WeaponHit(WsfWeaponEngagement aWeaponEngagement, WsfPlatform aTargetPlatform) 
   if (aWeaponEngagement.TargetResult() == "KILLED") # double check
   {
      string data;
      data = (string) TIME_NOW + "," + aWeaponEngagement.FiringPlatformName() + "," + 
#      aWeaponEngagement.FiringPlatform().Side() + "," + 
      (string) aWeaponEngagement.WeaponPlatform().Latitude() + "," + 
      (string) aWeaponEngagement.WeaponPlatform().Longitude() + "," + 
      (string) aWeaponEngagement.WeaponPlatform().AuxDataInt("GlobalTrackNumber")  + "," + 
      (string) aTargetPlatform.Name() + "," + 
      (string) aTargetPlatform.Latitude() + "," + 
      (string) aTargetPlatform.Longitude() + "," + "KILLED"; 
      eventData.PushBack(data);
   }
end_script

script void eventTimeline_WeaponMissed(WsfWeaponEngagement aWeaponEngagement, WsfPlatform aTargetPlatform) 
#   if (aWeaponEngagement.TargetResult() == "MISSED") # if missed, does not publish a target result
#   {
      string data;
      data = (string) TIME_NOW + "," + aWeaponEngagement.FiringPlatformName() + "," +
#      aWeaponEngagement.FiringPlatform().Side() + "," + 
      (string) aWeaponEngagement.WeaponPlatform().Latitude() + "," + 
      (string) aWeaponEngagement.WeaponPlatform().Longitude() + "," + 
      (string) aWeaponEngagement.WeaponPlatform().AuxDataInt("GlobalTrackNumber")  + "," + 
      (string) aTargetPlatform.Name() + "," + 
      (string) aTargetPlatform.Latitude() + "," + 
      (string) aTargetPlatform.Longitude() + "," + "MISSED"; 
      eventData.PushBack(data);
#   }
end_script

/*

script void eventTimeline_WeaponTerminated(WsfWeaponEngagement aWeaponEngagement, WsfPlatform aTargetPlatform) 
#   if (aWeaponEngagement.TargetResult() == "MISSED")
   {
      string data;
      data = (string) TIME_NOW + "," + aWeaponEngagement.FiringPlatformName() + "," +
      aWeaponEngagement.FiringPlatform().Side() + "," + 
      (string) aWeaponEngagement.WeaponPlatform().Latitude() + "," + 
      (string) aWeaponEngagement.WeaponPlatform().Longitude() + "," + 
      (string) aWeaponEngagement.WeaponPlatform().AuxDataInt("GlobalTrackNumber")  + "," + 
      (string) aTargetPlatform.Name() + "," + 
      (string) aTargetPlatform.Latitude() + "," + 
      (string) aTargetPlatform.Longitude() + "," + "MISSED"; 
      eventData.PushBack(data);
   }
end_script
*/

# Message Received
script void eventTimeline_MessageReceived(WsfComm aXmtr, WsfComm aRcvr, WsfMessage aMsg, WsfCommInteraction aResult)
   if(aMsg.Type() == "WSF_TRACK_MESSAGE")
   {
      WsfTrackMessage aTrackMessage = (WsfTrackMessage) aMsg;
      recordToEventTimeline(aXmtr.Platform(), 
                            aRcvr.Platform(), 
                            (string) aTrackMessage.Track().GlobalTrackNumber(),
                            aXmtr.Platform().Location(),
                            "TRACK MESSAGE RECEIVED");
   }
   if(aMsg.Type() == "WSF_CONTROL_MESSAGE")
   {
      WsfControlMessage aControlMessage = (WsfControlMessage) aMsg;
      recordToEventTimeline(aRcvr.Platform(), 
                            aControlMessage.Track().Target(), 
                            (string) aControlMessage.Track().GlobalTrackNumber(),
                            aXmtr.Platform().Location(),
                            "CONTROL MESSAGE RECEIVED");
   }
end_script

# Sensor track initiated
script void eventTimeline_SensorTrackInitiated(WsfPlatform aPlatform, WsfSensor aSensor, WsfTrack aTrack)
   if (!aTrack.IsValid())
      return;
   aTrack.SetGlobalTrackNumber(globalTrackNumber);
   globalTrackNumber += 1;
   recordToEventTimeline(aPlatform, 
                         aTrack.Target(), 
                         ((string) aTrack.GlobalTrackNumber()),
                         aTrack.Target().Location(),
                         "SENSOR TRACK INITIATED");
end_script

# Sensor Track Dropped
script void eventTimeline_SensorTrackDropped(WsfPlatform aPlatform, WsfSensor aSensor, WsfTrack aTrack)
   string data;
   data = (string) TIME_NOW + "," +
   aTrack.Originator().Name() + "," +
#   aTrack.Originator().Side() + "," +
   (string) aPlatform.Latitude() + "," + 
   (string) aPlatform.Longitude() + "," + 
   (string) aTrack.GlobalTrackNumber() + "," + 
   aTrack.TargetName() + "," +
   (string) aTrack.ReportedLocation().Latitude() + "," + 
   (string) aTrack.ReportedLocation().Longitude() + "," +
   "SENSOR TRACK DROPPED"; 
   if (data != "") eventData.PushBack(data);
end_script

# Message Transmitted
script void eventTimeline_MessageAttempt(WsfComm aXmtr, WsfComm aRcvr, WsfMessage aMsg, WsfCommInteraction aResult) 
   string data;
   if((aMsg.Type() == "WSF_TRACK_MESSAGE") && (aResult.Succeeded()))
   {
      WsfTrackMessage aTrackMessage = (WsfTrackMessage) aMsg;
      data = (string) TIME_NOW + "," +
      aXmtr.Platform().Name() + "," +
#      aXmtr.Platform().Side() + "," +
      (string) aXmtr.Platform().Latitude() + "," + 
      (string) aXmtr.Platform().Longitude() + "," + 
      (string) aTrackMessage.Track().GlobalTrackNumber() + "," + 
      aRcvr.Platform().Name() + "," + 
      (string) aRcvr.Platform().Latitude() + "," + 
      (string) aRcvr.Platform().Longitude() + "," +
      "TRACK MESSAGE TRANSMITTED"; 
      if(data !="") eventData.PushBack(data);
   }
   if((aMsg.Type() == "WSF_CONTROL_MESSAGE") && (aResult.Succeeded()))
   {
      WsfControlMessage aControlMessage = (WsfControlMessage) aMsg;
      data = (string) TIME_NOW + "," +
      aXmtr.Platform().Name() + "," +
#      aXmtr.Platform().Side() + "," +
      (string) aXmtr.Platform().Latitude() + "," + 
      (string) aXmtr.Platform().Longitude() + "," + 
      (string) aControlMessage.Track().GlobalTrackNumber() + "," + 
      aRcvr.Platform().Name() + "," + 
      (string) aRcvr.Platform().Latitude() + "," + 
      (string) aRcvr.Platform().Longitude() + "," +
      "CONTROL MESSAGE TRANSMITTED"; 
      eventData.PushBack(data);
   }
end_script

# Red Platform Created
script void eventTimeline_PlatformInitialized(WsfPlatform aPlatform) 
   if (aPlatform.Side() != "red") return; # Why is this only being used for RED platforms?
   string data;
   data = (string) TIME_NOW + "," +
   aPlatform.Name() + "," +
#   aPlatform.Side() + "," +
   (string) aPlatform.Latitude() + "," + 
   (string) aPlatform.Longitude() + "," + 
   "N/A" + "," + 
   "N/A" + "," +
   "N/A" + "," + 
   "N/A" + "," +
   "Red Platform Created"; 
   if (data != "") eventData.PushBack(data);
end_script

script void eventTimeline_SimulationComplete()
   string filename = outputPath;
   generateOutputFile(filename, eventDataHeaders, eventData, outputPath);
end_script

observer
   enable PLATFORM_INITIALIZED      eventTimeline_PlatformInitialized
   enable SIMULATION_COMPLETE       eventTimeline_SimulationComplete
   enable WEAPON_FIRED              eventTimeline_WeaponFired
   enable MESSAGE_RECEIVED          eventTimeline_MessageReceived          # Note the recieving platform is the target
   enable SENSOR_TRACK_INITIATED    eventTimeline_SensorTrackInitiated
   enable SENSOR_TRACK_DROPPED      eventTimeline_SensorTrackDropped
   enable WEAPON_HIT                eventTimeline_WeaponHit
   enable WEAPON_MISSED             eventTimeline_WeaponMissed             # Added to know when re-attack needed. What happens to the track?
   enable MESSAGE_DELIVERY_ATTEMPT  eventTimeline_MessageAttempt           # Platform and Target good
end_observer 