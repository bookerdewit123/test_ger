# File generated by Wizard 2.2205.6 on Nov 25, 2025.

include_once comm_network_names.txt

# Invoked by the CommManager during Initialization to register the Comm Networks
script Array<CommNetwork> DefineCommNetworks() {
   return {
      ConstructCommNetworkWithDelay( gcLINK16_NETWORK_NAME,
                                     G425_COMM_NETWORK_DELAY_TYPE().RANDOM_GAUSSIAN, 
                                     1.0, 1.0 ),
      ConstructCommNetworkWithDelay( gcMADL_NETWORK_NAME,
                                     G425_COMM_NETWORK_DELAY_TYPE().RANDOM_GAUSSIAN, 
                                     1.0, 1.0 )
   };
}
end_script

script void AddTrackToTrackManager(WsfPlatform aPlatform, WsfTrackMessage aTrackMsg){
   WsfTrackManager trackManager = aPlatform.TrackManager(); # Get the platform's TrackManager
   trackManager.AddTrackReport(aTrackMsg.Track());
}
end_script

# Non-C2 platform handling an incoming message
script void blueHandleCommMessage(CommMessage aCommMessage) {
   WsfMessage message = aCommMessage.GetMessage();
   if(message.Type() == "WSF_TRACK_MESSAGE") {
      WsfTrackMessage trackMsg = (WsfTrackMessage)message;
      if(message.SubType() == "") {
         # Add track reports to the track manager
         AddTrackToTrackManager(aCommMessage.GetTargetPlatform(), trackMsg);
      } else if(message.SubType() == "SHOOT") {
         # Attempt to shoot
         WsfPlatform shooter = aCommMessage.GetTargetPlatform();
         if( shooter.Weapon("aam").Fire(trackMsg.Track()) ) {
            # Report the shot
            string console_msg = (string)aCommMessage.GetTimestamp() + " Released weapon targeting " + trackMsg.Track().TargetName() + " [" + trackMsg.Track().TrackId().ToString() + "]";
            shooter.Comment(console_msg);
         } else {
            # If we can't fire, then notify the C2
            WsfMessage responseMsg = WsfMessage(message);
            responseMsg.SetSubType("FAILED_TO_FIRE");
            
            CommManagerInstance().SendMessageToTarget( aCommMessage.GetTimestamp(), 
                                                       aCommMessage.GetTargetPlatform(), 
                                                       aCommMessage.GetSourcePlatform(),
                                                       responseMsg );
         }
      }
   }
}
end_script


# C2 platform handling a message
script void blueC2HandleCommMessage(CommMessage aCommMessage) {

   WsfMessage message = aCommMessage.GetMessage();
   if(message.Type() == "WSF_TRACK_MESSAGE") {
      # Add track reports to the track manager
      WsfTrackMessage trackMessage = (WsfTrackMessage)message;
      AddTrackToTrackManager(aCommMessage.GetTargetPlatform(), trackMessage);
   }

   # Forward message to the BMS
   G425BlueBmsInstance().HandleCommMessage(aCommMessage);
}
end_script

